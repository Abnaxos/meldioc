/*
 *  Copyright (c) 2021 Raffael Herzog
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to
 *  deal in the Software without restriction, including without limitation the
 *  rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 *  sell copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 *  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 *  IN THE SOFTWARE.
 */

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'jacoco'

//noinspection UnnecessaryQualifiedReference
apply plugin: local.DependencySets

archivesBaseName = buildArtifactBaseName(project)

// as of 2021-05-14, Java 16 is not supported yet by Gradle
// 15 is OK though, we only need the language features up to sealed classes
java.toolchain.languageVersion = JavaLanguageVersion.of(15)
tasks.withType(JavaCompile) {
    compileJava.options.release.set 11
}

ext.slf4jVersion = '1.7.26'

dependencySets {
    slf4j '1.7.30', 'org.slf4j',
          'slf4j-api'
    log4j '2.13.1', 'org.apache.logging.log4j',
          'log4j-slf4j-impl', 'log4j-jul'
    jul slf4j.version, slf4j.group,
          'slf4j-jdk14'
    vavr '0.10.3', 'io.vavr', 'vavr'
    immutables '2.8.3', 'org.immutables', 'value-annotations', 'encode',
               [group: 'org.immutables.vavr', name: 'vavr-encodings', version: '0.6.0',
                configure: {exclude module: 'vavr'; exclude group: 'org.immutables'}]
    immutablesProc immutables.version, immutables.group,
                   'value-processor', immutables
    nullity '3.0.2', 'com.google.code.findbugs', 'jsr305'
    tsconfig '1.4.0', 'com.typesafe', 'config'
    gson '2.8.5', 'com.google.code.gson', 'gson'
    gsonVavr '0.10.2', vavr.group, 'vavr-gson'
    gsonImmutables immutables.version, immutables.group, 'gson'
}

configurations {
    // TODO (2019-12-10) workaround, remove when https://youtrack.jetbrains.com/issue/IDEA-228522 is fixed
    //annotationProcessor.extendsFrom compile, compileOnly

    testCompileOnly.extendsFrom compileOnly
    testRuntimeOnly.extendsFrom runtimeOnly
    spockReports
}

dependencies {
    compileOnly dependencySets.nullity

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5', {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.2.10'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.14', {
        exclude module: 'groovy-test-junit5'
        exclude module: 'groovy-testng'
    }

    testRuntimeOnly dependencySets.log4j
    testRuntimeOnly project(':shared-rt:log4j-config')
    // required for running tests in IDEA 2021.1:
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.7.1'

    spockReports group: 'com.athaydes', name: 'spock-reports', version: '1.6.2', {
        transitive false
    }
}

//noinspection GroovyAssignabilityCheck
task sourcesJar(type: Jar) {
    //noinspection GroovyAssignabilityCheck
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

javadoc {
    title = "$archivesBaseName $version API"
    options.addBooleanOption 'Xdoclint:-missing', true
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    archiveClassifier.set 'javadoc'
    from javadoc.destinationDir
}

ext.publishToMaven = true
ext.noMaven = { ext.publishToMaven = false }

afterEvaluate {
    if (ext.publishToMaven) {
        apply from: rootProject.file('maven.gradle')
    }
}

compileJava {
    options.compilerArgs += '-Xlint:all'
    options.compilerArgs +=
        ['-processing', '-serial', '-classfile'].collect {'-Xlint:' + it}
    def werror = project.properties['ch.raffael.meldioc.Werror'] == 'true'
    if (project.path.startsWith(':usecases')) {
        werror = false
    }
    if (werror) {
        options.compilerArgs += '-Werror'
    }
}

afterEvaluate {
    tasks.withType(Test).each { t ->
        def sourceRoots = (sourceSets.test.groovy.srcDirs + sourceSets.main.groovy.srcDirs).join(File.pathSeparator)
        t.jvmArgs += [
            "-Dcom.athaydes.spockframework.report.showCodeBlocks='false'",
            "-Dcom.athaydes.spockframework.report.testSourceRoots=$sourceRoots",
            "-Dcom.athaydes.spockframework.report.outputDir=$rootReportsDir${File.separator}spock-reports"]
        t.classpath = files(t.classpath, configurations.spockReports)
    }
}
jacoco {
    toolVersion = jacocoVersion
}
task registerJacoco() {
    doFirst {
        rootProject.tasks.jacocoReport.with {
            executionData.from fileTree("$buildDir/jacoco", {include '**/*.exec'}).files
            classDirectories.from sourceSets.main.output.classesDirs
            sourceDirectories.from sourceSets.main.java.sourceDirectories
        }
    }
}
afterEvaluate {
    test.finalizedBy registerJacoco
    registerJacoco.finalizedBy rootProject.jacocoReport
}

ext.enableMeld = {
    dependencies {
        compileOnly project(':annotations')
        annotationProcessor project(':tools:processor')
    }
}
ext.enableImmutables = {
    dependencies {
        compileOnly dependencySets.immutables
        annotationProcessor project(':util:immutables-proc')
    }
}
